apply plugin: 'com.android.application'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

def versionMajor = 2
def versionMinor = 1
def versionPatch = 0

def appVersionCode = versionMajor * 100 + versionMinor * 10 + versionPatch
def appVersionName = "${versionMajor}.${versionMinor}.${versionPatch}"

def appCompileSdkVersion = 28
def appPackageName = "com.cyder.android.syncpod"
def appMinSdkVersion = 19
def appTargetSdkVersion = 28

android {
    compileSdkVersion appCompileSdkVersion
    flavorDimensions "default"
    defaultConfig {
        applicationId appPackageName
        minSdkVersion appMinSdkVersion
        targetSdkVersion appTargetSdkVersion
        versionCode appVersionCode
        versionName appVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        def properties = new Properties()
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
        buildConfigField "String", "YOUTUBE_DEVELOPER_KEY", "\"${properties.getProperty("developerKey")}\""
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix '-dev'
        }
        release {
            minifyEnabled true
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            jniDebuggable false
            renderscriptDebuggable false
        }
    }

    productFlavors {
        develop {
            applicationId "${appPackageName}.develop"
        }
        production {
            applicationId "${appPackageName}"
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    dataBinding {
        enabled = true
    }

}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'
    implementation 'com.google.android.material:material:1.1.0-alpha05'

    implementation 'com.google.dagger:dagger:2.14.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.8'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.2.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'com.github.gfx.android.orma:orma:4.2.5'
    implementation 'com.squareup.okhttp3:okhttp:3.9.1'
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    implementation 'com.google.code.gson:gson:2.8.2'
    implementation 'androidx.core:core-ktx:1.0.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.22.5'
    implementation 'com.github.hosopy:actioncable-client-kotlin:0.0.2'
    implementation 'com.github.bumptech.glide:glide:4.6.1'
    implementation('com.google.apis:google-api-services-youtube:v3-rev186-1.23.0'){
        exclude module: 'httpclient'
        exclude group: 'org.apache.httpcomponents'
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
    configurations {
        all*.exclude group: 'com.google.guava', module: 'listenablefuture'
    }
    api files('libs/YouTubeAndroidPlayerApi.jar')
    kapt 'com.github.bumptech.glide:compiler:4.6.1'

    kapt 'com.google.dagger:dagger-compiler:2.14.1'
    kapt 'com.github.gfx.android.orma:orma-processor:4.2.5'
    kapt 'com.google.dagger:dagger-compiler:2.14.1'
    annotationProcessor 'androidx.databinding:databinding-compiler:3.4.0'

    compileOnly 'javax.annotation:jsr250-api:1.0'

}
