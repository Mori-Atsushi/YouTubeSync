apply plugin: 'com.android.application'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

def versionMajor = 0
def versionMinor = 1
def versionPatch = 0

def appVersionCode = versionMajor * 100 + versionMinor * 10 + versionPatch
def appVersionName = "${versionMajor}.${versionMinor}.${versionPatch}"

def appCompileSdkVersion = 26
def appPackageName = "com.cyder.atsushi.youtubesync"
def appMinSdkVersion = 19
def appTargetSdkVersion = 26

android {
    compileSdkVersion appCompileSdkVersion
    flavorDimensions "default"
    defaultConfig {
        applicationId appPackageName
        minSdkVersion appMinSdkVersion
        targetSdkVersion appTargetSdkVersion
        versionCode appVersionCode
        versionName appVersionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix '-dev'
            manifestPlaceholders = [developer_key: System.getenv('YOUTUBE_DEVELOPER_KEY')]
        }
        release {
            minifyEnabled true
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            jniDebuggable false
            renderscriptDebuggable false
            manifestPlaceholders = [developer_key: System.getenv('YOUTUBE_DEVELOPER_KEY')]
        }
    }

    productFlavors {
        develop {
            applicationId "${appPackageName}.develop"
        }
        production {
            applicationId "${appPackageName}"
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    dataBinding {
        enabled = true
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'com.android.support:appcompat-v7:26.+'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'

    implementation 'com.google.dagger:dagger:2.14.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.8'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.2.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'com.github.gfx.android.orma:orma:4.2.5'
    implementation 'com.squareup.okhttp3:okhttp:3.9.1'
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.google.code.gson:gson:2.8.2'

    kapt 'com.google.dagger:dagger-compiler:2.14.1'
    kapt 'com.github.gfx.android.orma:orma-processor:4.2.5'
    kapt 'com.google.dagger:dagger-compiler:2.14.1'
    kapt 'com.android.databinding:compiler:3.0.1'

    compileOnly 'javax.annotation:jsr250-api:1.0'
}
